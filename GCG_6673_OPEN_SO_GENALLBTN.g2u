Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Caption("SO Watch")
Gui.Form..Size(15360,10695)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..ControlBox(False)
Gui.Form..MaxButton(False)
Gui.Form..MinButton(False)
Gui.Form..MousePointer(0)
Gui.Form..Moveable(False)
Gui.Form..Sizeable(False)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

'ARC#6673
'Customer:DIVERSIFIED LABELING SOLUTIONS
'	Mar 2022, Nam Le


'The core system for customer ship hold effects all orders by customer which interferes with DLS
'process of being able to create WOs and Ship orders in a more streamline fashion. DLS would like the
'ship hold process to function at an order level and not at a customer level. Additionally, they have 3
'types of customers: ones that would never get put on Ship Hold, ones that always get put on ship hold
'regardless of the open balance, and ones that get put on ship hold only when they exceed their credit
'limit.

'hook#11565 - ORDER SELECTED
'---- Change Script 3 button name to Gen All WO--------------
'check if the SO is on hold, if not enable the btn
'if on hold, disable the btn
Program.Sub.Main.Start
f.Intrinsic.Control.Try
	v.Local.sSQL.Declare(String)
	v.Local.sSO.Declare(String)
	v.Local.sRet.Declare(String)
	v.Local.sCust.Declare(String)
	
	'F.Intrinsic.Control.If(V.Caller.Switches,<>,"V")
	V.Passed.000203.Set("Gen All WO")
	
	'check hold status => enable/disable
	'if flag = 0/2/4 => hold
	'------------------------------------------------------------
	v.Local.sSO.Set(v.Passed.000003)
	f.Intrinsic.String.LPad(v.Local.sSO,"0",7,v.Local.sSO)
	v.Local.sCust.Set(v.Passed.000006)
	
	
	f.Intrinsic.String.Build("select SHP_HLD_FLAG from GCG_6673_SO_HOLD where RTRIM(ORDER_NO)='{0}'", v.Local.sSO, v.Local.sSQL)
	f.ODBC.Connection!Con.OpenCompanyConnection
		f.ODBC.Connection!Con.ExecuteAndReturn(v.Local.sSQL, v.Local.sRet)
	f.ODBC.Connection!Con.Close
	
	f.Intrinsic.Control.If(v.Ambient.ExecuteAndReturnEOF,=,True)
		f.Intrinsic.UI.ChangeCallerProperty(v.Passed.000203, "LOCK", 1)
	f.Intrinsic.Control.Else
		f.Intrinsic.Control.If(v.Local.sRet,=,"0", "or", v.Local.sRet,=,"2", v.Local.sRet,=,"4")
			f.Intrinsic.UI.ChangeCallerProperty(v.Passed.000203, "LOCK", 1)
		f.Intrinsic.Control.Else
			f.Intrinsic.UI.ChangeCallerProperty(v.Passed.000203, "LOCK", 0)
		f.Intrinsic.Control.EndIf
	f.Intrinsic.Control.EndIf
	'------------------------------------------------------------
	
	'Feb 2023 - also refresh the data in the GCG_6673_CRR_SO table, for when user opens an SO, then click on save, the current record could point to a different SO
	'find current total amount of the SO
	'delete old record
	'insert the new record for current SO
	'------------------------------------------------------------------------
	f.ODBC.Connection!Con.OpenCompanyConnection
		'find TOTAL_AMT
		f.Intrinsic.String.Build("select SUM(QTY_ORDERED*PRICE) from V_ORDER_LINES where RTRIM(ORDER_NO)='{0}'", v.Local.sSO, v.Local.sSQL)
		v.Local.sRet.Set("")
		f.ODBC.Connection!Con.ExecuteAndReturn(v.Local.sSQL, v.Local.sRet)
		f.Intrinsic.Control.If(v.Local.sRet,=,"")
			v.Local.sRet.Set(0)
		f.Intrinsic.Control.EndIf
		
		'delete old record and insert new
		f.Intrinsic.String.Build("delete from GCG_6673_CRR_SO where RTRIM(GSSUSER)='{0}'", v.Caller.User, v.Local.sSQL)
		f.ODBC.Connection!Con.Execute(v.Local.sSQL)
		
		f.Intrinsic.String.Build("insert into GCG_6673_CRR_SO (ORDER_NO, CUSTOMER, TOTAL_AMT, GSSUSER) values('{0}','{1}',{2},'{3}')", v.Local.sSO, v.Local.sCust, v.Local.sRet, v.Caller.User, v.Local.sSQL)
		f.ODBC.Connection!Con.Execute(v.Local.sSQL)
		
	f.ODBC.Connection!Con.Close
	'------------------------------------------------------------------------
	
f.Intrinsic.Control.Catch
	f.Intrinsic.UI.Msgbox(v.Ambient.ErrorDescription)
f.Intrinsic.Control.EndTry

Program.Sub.Main.End

Program.Sub.Comments.Start
${$5$}$2.0.0.0$}$2
${$6$}$nle$}$20230221091937905$}$kjzCRYkA6UEoRnCwndYnRSjyVXnS1VGFay3Ln27JKMEGr42PX4KUF4kQuuTudac8
Program.Sub.Comments.End